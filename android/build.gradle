buildscript {
  repositories {
    mavenCentral()
    google()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:8.1.0'
  }
}

allprojects {
  repositories {
    mavenCentral()
    maven {
      url "https://maven.google.com"
    }
  }

  gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
      // Using deprecated API is ok, since we want to support old Android
      // versions.
      // options.compilerArgs << "-Xlint:deprecation"
      options.compilerArgs << "-Xlint:unchecked"
    }
  }
}

apply plugin: 'com.android.application'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
  namespace 'sk.breviar.android'

  compileSdkVersion 33
  
  defaultConfig {
    targetSdkVersion 33
      // TODO: api 33 has changes in notifications: POST_NOTIFICATIONS permission
    minSdkVersion 14
    ndk {
      // Tells Gradle to build outputs for the following ABIs and package
      // them into your APK.
      abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
    }
  }

  /*
   * TODO(riso): This would work fine, but recent NDK (at least since 15c)
   * support only android-14 (android 4.0.0) or later. I am still somewhat
   * reluctant to drop support for older devices.
   */
  externalNativeBuild {
    ndkBuild {
      path "jni/Android.mk"
    }
  }

  sourceSets {
    main {
      manifest.srcFile 'AndroidManifest.xml'
      java.srcDirs = ['src']
      resources.srcDirs = ['src']
      aidl.srcDirs = ['src']
      renderscript.srcDirs = ['src']
      res.srcDirs = ['res']
      assets.srcDirs = ['assets']
    }
    // Overrides for debug releases.
    debug {
      res.srcDirs = ['res.debug']
    }
    // Placeholders for values introduced in debug builds.
    release {
      res.srcDirs = ['res.release']
    }
  }

  signingConfigs {
    release {
      keyAlias keystoreProperties['keyAlias']
      keyPassword keystoreProperties['keyPassword']
      storeFile file(keystoreProperties['storeFile'])
      storePassword keystoreProperties['storePassword']
    }
  }
  buildTypes {
    release {
      signingConfig signingConfigs.release
    }
  }
}

dependencies {
  implementation 'com.android.support:support-v4:27.0.0'
  implementation 'com.android.support:support-core-utils:27.0.0'
  implementation 'com.android.support:design:27.0.0'
  implementation 'com.android.support:appcompat-v7:27.0.0'
  implementation 'com.android.support:support-media-compat:27.0.0'
}
